
## <summary>policy for ipa_custodia</summary>

########################################
## <summary>
##	Execute ipa_custodia_exec_t in the ipa_custodia domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`ipa_custodia_domtrans',`
	gen_require(`
		type ipa_custodia_t, ipa_custodia_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, ipa_custodia_exec_t, ipa_custodia_t)
')

######################################
## <summary>
##	Execute ipa_custodia in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`ipa_custodia_exec',`
	gen_require(`
		type ipa_custodia_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, ipa_custodia_exec_t)
')

########################################
## <summary>
##	Search ipa_custodia conf directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`ipa_custodia_search_conf',`
	gen_require(`
		type ipa_custodia_etc_rw_t;
	')

	allow $1 ipa_custodia_etc_rw_t:dir search_dir_perms;
	files_search_etc($1)
')

########################################
## <summary>
##	Read ipa_custodia conf files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`ipa_custodia_read_conf_files',`
	gen_require(`
		type ipa_custodia_etc_rw_t;
	')

	allow $1 ipa_custodia_etc_rw_t:dir list_dir_perms;
	read_files_pattern($1, ipa_custodia_etc_rw_t, ipa_custodia_etc_rw_t)
	files_search_etc($1)
')

########################################
## <summary>
##	Manage ipa_custodia conf files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`ipa_custodia_manage_conf_files',`
	gen_require(`
		type ipa_custodia_etc_rw_t;
	')

	manage_files_pattern($1, ipa_custodia_etc_rw_t, ipa_custodia_etc_rw_t)
	files_search_etc($1)
')


########################################
## <summary>
##	All of the rules required to administrate
##	an ipa_custodia environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`ipa_custodia_admin',`
	gen_require(`
		type ipa_custodia_t;
		type ipa_custodia_etc_rw_t;
	')

	allow $1 ipa_custodia_t:process { signal_perms };
	ps_process_pattern($1, ipa_custodia_t)

    tunable_policy(`deny_ptrace',`',`
        allow $1 ipa_custodia_t:process ptrace;
    ')

	files_search_etc($1)
	admin_pattern($1, ipa_custodia_etc_rw_t)
	optional_policy(`
		systemd_passwd_agent_exec($1)
		systemd_read_fifo_file_passwd_run($1)
	')
')

########################################
## <summary>
##	Create a named socket in a httpd_var_run_t directory.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`ipa_custodia_manage_httpd_sockets',`
	gen_require(`
		type httpd_var_run_t;
	')

	manage_sock_files_pattern($1, httpd_var_run_t, httpd_var_run_t)
	manage_dirs_pattern($1, httpd_var_run_t, httpd_var_run_t)
')

########################################
## <summary>
##      Allow read pki tomcat etc files.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed access.
##      </summary>
## </param>
#
interface(`ipa_custodia_read_tomcat_etc_rw',`
        gen_require(`
				type pki_tomcat_etc_rw_t;
        ')

        read_files_pattern($1, pki_tomcat_etc_rw_t, pki_tomcat_etc_rw_t)
        list_dirs_pattern($1, pki_tomcat_etc_rw_t, pki_tomcat_etc_rw_t)
')

########################################
## <summary>
##	Connect to httpd over an unix
##	stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`ipa_custodia_httpd_stream_connect',`
	gen_require(`
		type httpd_t;
	')

allow httpd_t $1:unix_stream_socket connectto;
')

#######################################
## <summary>
##      Read clamd state files.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed access.
##      </summary>
## </param>
#
interface(`ipa_custodia_read_state_httpd',`
        gen_require(`
                type httpd_t;
        ')

        kernel_search_proc($1)
        ps_process_pattern($1, httpd_t)
')
